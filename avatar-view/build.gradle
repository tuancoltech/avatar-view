apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

apply from: "$rootDir/versions.gradle"
android {
    namespace 'agency.tango.android.avatarview'
    compileSdk 34
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            defaultPublishConfig "debug"
        }
    }

    publishing {
        singleVariant("debug") {
            withSourcesJar()
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.jakewharton.timber:timber:4.3.1'
    implementation "androidx.annotation:annotation:1.9.1"
}

def libVersion = '0.0.2'
def localPropertiesFile = project.rootProject.file("local.properties")
def githubUser = null
def githubToken = null

if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localPropertiesFile.withInputStream { localProperties.load(it) }
    githubUser = localProperties.getProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
    githubToken = localProperties.getProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
} else {
    githubUser = System.getenv("GITHUB_ACTOR")
    githubToken = System.getenv("GITHUB_TOKEN")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "agency.tango.android"
            artifactId = "avatarview"
            version = libVersion
            afterEvaluate {
                from(components["debug"])
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/tuancoltech/avatar-view")
            credentials {
                username = githubUser
                password = githubToken
            }
        }
    }
}
