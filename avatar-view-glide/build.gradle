apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

apply from: "$rootDir/versions.gradle"
android {
    namespace 'agency.tango.android.avatarviewglide'
    compileSdk 34
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            defaultPublishConfig "debug"
        }
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant("debug") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation "androidx.annotation:annotation:1.9.1"
    implementation project(path: ':avatar-view')
}


def libVersion = '0.0.2'

def localPropertiesFile = project.rootProject.file("local.properties")
def githubUser = null
def githubToken = null

if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localPropertiesFile.withInputStream { localProperties.load(it) }
    githubUser = localProperties.getProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
    githubToken = localProperties.getProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
} else {
    githubUser = System.getenv("GITHUB_ACTOR")
    githubToken = System.getenv("GITHUB_TOKEN")
}


publishing {
    publications {
        release(MavenPublication) {
            groupId = "agency.tango.android"
            artifactId = "avatar-view-glide"
            version = libVersion
            afterEvaluate {
                from(components["debug"])
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/tuancoltech/avatar-view")
            credentials {
                username = githubUser
                password = githubToken
            }
        }
    }
}